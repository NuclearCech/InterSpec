apply plugin: 'com.android.application'


//def my_prefix = "/Users/wcjohns/install/android"
def my_prefix = local_props("lib.prefix")
def keystore_file = local_props("keystore.location")
def keystore_pword = local_props("keystore.password")
def keystore_alias_name = local_props("keystore.alias.name")
def keystore_alias_pword = local_props("keystore.alias.password")


android {
    signingConfigs {
        config {
            keyAlias keystore_alias_name
            keyPassword keystore_alias_pword
            storeFile file(keystore_file)
            storePassword keystore_pword
        }
    }
    compileSdkVersion 27
    defaultConfig {
        applicationId "gov.sandia.interspec"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 9
        versionName "1.0.3"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cppFlags "-std=c++11"
                arguments "-DANDROID_CPP_FEATURES=rtti exceptions",
                        "-DANDROID_TOOLCHAIN=clang",
                        "-DANDROID_PIE=ON",
                        "-DANDROID_STL=c++_static",
                        "-DBOOST_WT_PREFIX_BASE=" + my_prefix
            }
        }
        signingConfig signingConfigs.config
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            signingConfig signingConfigs.config
        }
    }
    externalNativeBuild {
        cmake {
            path "../../../../CMakeLists.txt"
        }
    }
    flavorDimensions 'cpuArch'
    productFlavors {
        arm7 {
            dimension 'cpuArch'
            ndk {
                abiFilter 'armeabi-v7a'
            }
        }
        arm8 {
            dimension 'cpuArch'
            ndk {
                abiFilters 'arm64-v8a'
            }
        }
        x86 {
            dimension 'cpuArch'
            ndk {
                abiFilter 'x86'
            }
        }
        x86_64 {
            dimension 'cpuArch'
            ndk {
                abiFilter 'x86_64'
            }
        }
        universal {
            dimension 'cpuArch'
            // include all default ABIs. with NDK-r16,  it is:
            //   armeabi-v7a, arm64-v8a, x86, x86_64
        }
    }
    buildToolsVersion '27.0.3'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

//------------------------------------------------------------
def local_props(key) { // reads ~ value from local.properties
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    def val = properties.getProperty(key, null)
    if (val == null)
        throw new GradleException("""\
                cant find value '${key}' in file 'local.properties'.  """)

    return val
}